# 声明插件
Gradle插件分为内置插件与社区插件，内置插件ID以 `org.gradle` 开头，例如： `org.gradle.java-library` 。我们声明内置插件时不能指定版本，因为其版本跟随当前工程所使用的Gradle版本；并且不必在顶级配置文件中声明插件，由各个子模块自行声明即可。

社区插件由第三方开发，通常会提交到Gradle社区插件仓库，相关信息可以在 [🔗 Gradle - 社区插件仓库](https://plugins.gradle.org/) 网站查询，我们使用社区插件时必须先在顶级配置文件中声明插件ID与版本，然后在需要应用插件的子模块配置文件中再次声明插件。

若要为模块应用插件，我们需要在其 `build.gradle` 配置文件的 `plugins {}` 小节中进行声明，常见的插件声明语法如下文列表所示：

- `id("org.jetbrains.kotlin.jvm").version("1.9.25")` : 这是最常见的插件声明语法，通过插件ID与版本号进行声明。
- `` `kotlin-dsl` `` : 这是内置插件的声明方式，实际上是引用了Gradle提供的名为 `kotlin-dsl` 的变量，等价于 `id("org.gradle.kotlin.kotlin-dsl")` 。
- `kotlin("jvm")` : 这是Kotlin相关插件提供的快捷语法，括号内的内容会自动加上 `libs.plugins.kotlin` 前缀，等价于 `id("org.jetbrains.kotlin.jvm")` 。
- `alias(libs.plugins.kotlin.jvm)` : 引用Version Catalog定义的插件别名与版本号，详见相关章节 [🧭 依赖管理 - Version Catalog](04_依赖管理.md#version-catalog) 。


# Maven Publish
## 简介
Maven Publish是一个Gradle插件，它的功能是将模块编译产生的JAR或AAR文件上传至Maven仓库，以便被其他工程引用。

Maven Publish为Gradle新增了以下任务：

🔷 `publish`

执行所有的发布任务，将组件发布到远程Maven仓库。

🔷 `publishToMavenLocal`

执行所有的发布任务，将组件发布到本地Maven仓库。

🔷 `publish<发布配置名称>PublicationToRepoNameRepository`

执行指定的发布任务，将组件发布到远程Maven仓库。

如果我们在创建发布配置时没有指定名称，该名称默认为"Maven"。

🔷 `publish<发布配置名称>PublicationToMavenLocal`

执行指定的发布任务，将组件发布到远程Maven仓库。

🔷 `generatePomFileFor<发布配置名称>Publication`

生成指定发布任务对应的POM文件，目标路径为 `<模块根目录>/build/publications/<发布配置名称>/pom-default.xml` 。

## 基本应用
在下文示例中，我们为一个Java Library模块配置Maven Publish插件，使其能够将编译产生的JAR包发布至Maven仓库。

"build.gradle":

```groovy
plugins {
    id('java-library')
    // 引入Maven Publish插件
    id('maven-publish')
}

// Maven Publish插件配置
publishing {
    // 发布目标仓库配置
    repositories {
        // 添加远端Maven仓库
        maven {
            url = '<仓库地址>'
            credentials {
                username = '<用户名称>'
                password = '<登录口令>'
            }
        }
    }

    // 发布参数配置
    publications {
        // 创建名为"maven"的发布配置
        maven(MavenPublication) {
            // 产物的基本信息
            groupId = '<Group名称>'
            artifactId = '<Artifact名称>'
            version = '<版本号>'

            // 发布程序包
            from components.java

            // POM信息
            pom {
                // 打包格式
                packaging = 'jar'

                name = '<工程名称>'
                url = '<工程对应的网站>'
            }
        }
    }
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
plugins {
    id("java-library")
    // 引入Maven Publish插件
    id("maven-publish")
}

// Maven Publish插件配置
publishing {
    // 发布目标仓库配置
    repositories {
        // 添加远端Maven仓库
        maven {
            setUrl("<仓库地址>")
            credentials {
                username = "<用户名称>"
                password = "<登录口令>"
            }
        }

        // 添加本地Maven仓库
        mavenLocal()
    }

    // 发布参数配置
    publications {
        // 创建名为"maven"的发布配置
        create<MavenPublication>("maven") {
            // 产物的基本信息
            groupId = "<Group名称>"
            artifactId = "<Artifact名称>"
            version = "<版本号>"

            // 发布程序包
            from(components.getByName("java"))

            // POM信息
            pom {
                // 打包格式
                packaging = "jar"

                name.set("<工程名称>")
                url.set("<工程对应的网站>")
            }
        }
    }
}
```

Maven Publish插件的主要配置项都在 `publishing {}` 小节中，其中 `repositories {}` 小节用于配置目标仓库信息，语法与组件依赖仓库配置完全相同，详见相关章节： [🧭 依赖管理 - 仓库配置](./03_依赖管理.md#仓库配置) 。 `publications {}` 小节用于配置发布参数，控制需要发布的产物和生成的POM信息。

此处的 `groupId` 、 `artifactId` 、`version` 分别为当前模块的Group名称、Artifact名称和版本号，均为必填项； `from components.java` 语句指明了需要发布Java代码编译后产生的JAR包； `pom {}` 小节则声明了POM信息，其中 `packaging` 属性要与编译产物类型保持一致，其他属性则可根据实际需要选择性地填写。

此时我们可以在终端中执行 `gradle publish` 命令，Gradle将会开始编译任务，并在编译成功后将产生的JAR包上传到目标仓库中。

```text
[bi4vmr@Fedora Project]# ./gradlew publish
BUILD SUCCESSFUL in 1s
12 actionable tasks: 11 executed, 1 up-to-date
```

为了确保当前模块的依赖组件也能被调用者正确解析，Maven Publish插件会将组件信息写入POM文件，我们应当选择合适的依赖指令，确保组件在POM中的Scope属性符合预期：

<div align="center">

|  Gradle依赖指令  |       POM Scope属性       |
| :--------------: | :-----------------------: |
| `implementation` |         `runtime`         |
|      `api`       |         `compile`         |
|  `compileOnly`   | 此类依赖项不会被写入POM。 |
|  `runtimeOnly`   |         `runtime`         |

</div>

通常情况下，库的依赖组件也应当能够被调用者访问，因此我们建议使用 `api` 声明依赖组件。

## 发布源码包
在前文示例中，Maven Publish插件只会上传当前模块的字节码（JAR包）及组件依赖信息（包含在POM文件中），使用者在IDE中无法查看源码。

如果模块的代码允许公开，我们可以为Maven Publish插件添加一些配置项，在发布程序包的同时附带源码包，方便使用者查看源码。

"build.gradle":

```groovy
java {
    // 编译模块时同时打包源码
    withSourcesJar()
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
java {
    // 编译模块时同时打包源码
    withSourcesJar()
}
```

如果我们在发布配置中使用 `from components.java` 语句发布编译产物，此时只需在 `java {}` 小节中指明打包源码即可，Maven Publish插件可以自动上传后缀为"-sources.jar"的源码包；如果我们使用其他编译产物发布语句，或者希望改变源码包的名称、打包内容等，就要自行创建打包源码的任务：

"build.gradle":

```groovy
// 打包源码的任务
def sourcesJar = tasks.register('sourcesJar', Jar) {
    // 将"src/main"目录下的源码文件打包
    from sourceSets.main.allJava
    // 为源码包添加后缀与程序包作区分
    archiveClassifier = 'sources'
}

publishing {
    publications {
        // 创建名为"maven"的发布配置
        maven(MavenPublication) {
            // 此处省略其他配置项 ...

            // 发布源码包
            artifact sourcesJar
        }
    }
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
// 发布源码包的任务
val sourcesJar by tasks.creating(Jar::class) {
    // 将"src/main"目录下的源码文件打包
    from(sourceSets.getByName("main").allJava.srcDirs)
    // 为源码包添加后缀与程序包作区分
    archiveClassifier.set("sources")
}

publishing {
    publications {
        // 创建名为"maven"的发布配置
        create<MavenPublication>("maven") {
            // 此处省略其他配置项 ...

            // 发布源码包
            artifact(sourcesJar)
        }
    }
}
```

在上述示例代码中，我们创建了名为 `sourcesJar` 的任务以生成源码包，然后在 `publishing {}` 小节中通过 `artifact()` 方法应用该任务，将源码包也发布至仓库。

## 发布文档包
Maven Publish插件支持发布JavaDoc文档到Maven仓库中，如果我们需要实现此功能，可以在Gradle配置文件中添加以下配置：

"build.gradle":

```groovy
java {
    // 编译模块时同时打包文档
    withJavadocJar()
}

// 可选配置，根据实际需要添加即可。
javadoc {
    // 指定JavaDoc编码，解决系统编码与文件不一致导致错误。
    options.encoding = 'UTF-8'
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
java {
    // 编译模块时同时打包文档
    withJavadocJar()
}

// 可选配置，根据实际需要添加即可。
tasks.withType<Javadoc> {
    // 指定JavaDoc编码，解决系统编码与文件不一致导致错误。
    options.encoding = "UTF-8"
}
```

## 发布POM包
有些模块中不包含任何程序代码，只有POM文件，这种模块的作用是聚合多个依赖项，使用者只需依赖该模块，就会间接依赖其他组件。

由于没有程序代码，我们也不需要在发布配置中书写 `from components.java` 语句，此时插件不会自动在POM文件中生成 `<dependencies/>` 节点，因此我们需要新增一些逻辑代码，遍历Gradle的依赖组件，将其转为XML格式并写入POM文件。

"build.gradle":

```groovy
dependencies {
    implementation '<组件依赖项1>'
    implementation '<组件依赖项2>'
    implementation '<组件依赖项N>'
}

publishing {
    publications {
        maven(MavenPublication) {
            // 此处省略其他配置项 ...

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // 获取当前模块的所有"implementation"节点
                configurations.implementation.allDependencies.forEach() { Dependency dependency ->
                    def group = dependency.group
                    def artifact = dependency.name
                    def version = dependency.version
                    println("Parse dependency item. Group:[$group] Artifact:[$artifact] Version:[$version]")

                    // 向POM中添加依赖项，并跳过组名与版本号为空的项。
                    if (group != null && version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', group)
                        dependencyNode.appendNode('artifactId', artifact)
                        dependencyNode.appendNode('version', version)
                        dependencyNode.appendNode('scope', 'compile')
                    }
                }
            }
        }
    }
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
dependencies {
    implementation("<组件依赖项1>")
    implementation("<组件依赖项2>")
    implementation("<组件依赖项N>")
}

publishing {
    publications {
        create<MavenPublication>("maven") {
            // 此处省略其他配置项 ...

            pom.withXml {
                val dependenciesNode = asNode().appendNode("dependencies")
                // 获取当前模块的所有"implementation"节点
                configurations.getByName("implementation").allDependencies.forEach { dependency ->
                    val group: String? = dependency.group
                    val artifact: String = dependency.name
                    val version: String? = dependency.version
                    println("Parse dependency item. Group:[$group] Artifact:[$artifact] Version:[$version]")

                    // 向POM中添加依赖项，并跳过组名与版本号为空的项。
                    if (group != null && version != null) {
                        val dependencyNode = dependenciesNode.appendNode("dependency")
                        dependencyNode.appendNode("groupId", group)
                        dependencyNode.appendNode("artifactId", artifact)
                        dependencyNode.appendNode("version", version)
                        dependencyNode.appendNode("scope", "compile")
                    }
                }
            }
        }
    }
}
```

## 调整模块发布顺序
### 简介
部分模块之间存在依赖关系，例如模块 `ModuleA` 依赖远程仓库中的模块 `ModuleB` ，当我们将两个模块都升级到新的版本后，执行整个工程的发布任务就有可能遇到错误；这是因为Gradle会并行执行任务，如果 `ModuleA` 的发布任务首先被执行，此时 `ModuleB` 的新版本还没有被发布到远程仓库， `ModuleA` 就会因缺少依赖组件而编译失败。

下文章节提供了一些调整Gradle任务执行顺序的方法，它们具有不同的适用场景，我们可以根据实际情况进行选择。

### 指定任务执行顺序
我们可以在模块 `ModuleA` 的Gradle配置文件中添加以下语句，获取模块 `ModuleA` 的"publish"任务，然后指定该任务必须在它所依赖的模块 `ModuleB` 发布完成之后再执行。

"build.gradle":

```groovy
task(":publish") {
    dependsOn ':ModuleB:publish'
    mustRunAfter ':ModuleB:publish'
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
tasks.named("publish") {
    dependsOn(":ModuleB:publish")
    mustRunAfter(":ModuleB:publish")
}
```

`dependsOn()` 方法指定了执行模块 `ModuleA` 的"publish"任务时需要同时执行模块 `ModuleB` 的"publish"任务，此时两个模块的"publish"任务已经产生了关联，但它们仍然会并发执行。我们还需要调用 `mustRunAfter()` 方法，指定模块 `ModuleA` 的"publish"任务在模块 `ModuleB` 的"publish"任务执行完毕之后再开始执行。

这种方式使用了Gradle自身提供的能力，侵入性较低，但是作用范围仅限当前工程，无法控制跨工程任务的顺序。

### 手动填写依赖信息
我们可以在模块 `ModuleA` 的Gradle配置文件中添加以下语句，将模块 `ModuleB` 的远程仓库依赖声明改为本地工程依赖声明，此时模块 `ModuleA` 可以直接引用模块 `ModuleB` 中的代码通过编译。

"build.gradle":

```groovy
dependencies {
    // 将远程仓库依赖替换为本地工程依赖
    // implementation '<Group>:ModuleB:1.0'
    implementation project(':ModuleB')
}

publishing {
    publications {
        maven(MavenPublication) {
            // 此处省略其他配置项 ...

            pom.withXml {
                def group = '<组名称>'
                def artifact = '<产物名称>'
                def version = '<版本号>'

                def dependenciesNode = asNode().appendNode('dependencies')
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', group)
                dependencyNode.appendNode('artifactId', artifact)
                dependencyNode.appendNode('version', version)
            }
        }
    }
}
```

上述内容也可以使用Kotlin语言编写：

"build.gradle.kts":

```kotlin
dependencies {
    // 将远程仓库依赖替换为本地工程依赖
    // implementation("<Maven仓库路径>:ModuleB:1.0")
    implementation(project(":ModuleB"))
}

publishing {
    publications {
        create<MavenPublication>("maven") {
            // 此处省略其他配置项 ...

            pom.withXml {
                val group: String = "<组名称>"
                val artifact: String = "<产物名称>"
                val version: String = "<版本号>"

                val dependenciesNode = asNode().appendNode("dependencies")
                val dependencyNode = dependenciesNode.appendNode("dependency")
                dependencyNode.appendNode("groupId", group)
                dependencyNode.appendNode("artifactId", artifact)
                dependencyNode.appendNode("version", version)
            }
        }
    }
}
```

由于Maven Publish插件不会将本地工程依赖信息写入POM文件，我们还需要手动添加模块 `ModuleB` 的依赖信息。

这种方式需要手动管理依赖信息，较为繁琐且容易出现错漏，我们可以在其他方法失效时使用它。

### 通过编译脚本控制
如果整个工程由多个子工程组成，我们可以通过Bash、Python等脚本控制各个子工程的编译顺序，预先编译并发布被其他模块所依赖的基础模块。

这种方式需要编写额外的编译脚本，但能够解决跨工程的编译顺序问题，比较适合在规模较大的多模块工程中使用。
